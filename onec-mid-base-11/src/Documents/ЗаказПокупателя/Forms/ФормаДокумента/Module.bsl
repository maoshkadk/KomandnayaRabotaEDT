
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	//Котченко. Добавление поля Контактное лицо
	ЭлементКонтактноеЛицо = ЭтотОбъект.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), ЭтотОбъект.Элементы.ГруппаШапкаПраво);
	ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	//
	//
	//Котченко. Добавление поля Скидка 
	ЭлементСогласованнаяСкидка = ЭтотОбъект.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), ЭтотОбъект.Элементы.ГруппаШапкаЛево);
	ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.ДЗ_Скидка";
	ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении","ВыполнитьДействиеПриИзмененииСогласованнаяСкидка");
	//
	
	//Котченко. Добавление кнопки "УстановитьСкидкуСкидку"	
	КомандаУстановитьСкидку = Команды.Добавить("УстановитьСкидку");
	КомандаУстановитьСкидку.Заголовок = "Установить скидку";
	КомандаУстановитьСкидку.Действие = "КомандаУстановитьСкидкуДействие";
	
	КнопкаУстановитьСкидку = Элементы.Добавить("КнопкаУстановитьСкидку", Тип("КнопкаФормы"), ЭтотОбъект.Элементы.ГруппаШапкаЛево);
	КнопкаУстановитьСкидку.ИмяКоманды = "УстановитьСкидку";
	КнопкаУстановитьСкидку.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	//
	
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Котченко.Действие при изменении значения в согласованной скидке
&НаКлиенте
Асинх Процедура ВыполнитьДействиеПриИзмененииСогласованнаяСкидка()
	
	Если Объект.ДЗ_Скидка > 100 тогда
		Объект.ДЗ_Скидка = 100;
	КонецЕсли;
	
	Если Объект.ДЗ_Скидка < 0 тогда
		Объект.ДЗ_Скидка = 0;
	КонецЕсли;
	
	Если Объект.Товары.Количество() > 0 ИЛИ Объект.Услуги.Количество() > 0 Тогда
         Ответ = Ждать ВопросАсинх(НСтр("ru = 'пересчитать суммы с учетом скидки?'"), РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да тогда 
			ПересчитатьСуммы();
		Иначе Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
//

//Котченко. Математические операции для перерасчета скидки
&НаКлиенте
Процедура ПересчитатьСуммы()
	Товары = Объект.Товары;
	Услуги = Объект.Услуги;
	Для Каждого СтрокаТовары из Товары Цикл
		РассчитатьСуммуСтроки(СтрокаТовары);
	КонецЦикла;
	Для Каждого СтрокаУслуги из Услуги цикл
		РассчитатьСуммуСтроки(СтрокаУслуги);
	КонецЦикла;
	РассчитатьСуммуДокумента()
	
 КонецПроцедуры 
//


&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - (ТекущиеДанные.Скидка + Объект.ДЗ_Скидка) / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
